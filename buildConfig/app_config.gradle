import com.ghl.plugin.ModuleServicePlugin
import com.ghl.plugin.RouterPlugin

ext {
    android = [
            compileSdkVersion        : 29,
            minSdkVersion            : 21,
            targetSdkVersion         : 29,
            versionCode              : 1,
            versionName              : "1.0",
            buildToolsVersion        : "29.0.2",
            testInstrumentationRunner: "androidx.test.runner.AndroidJUnitRunner"
    ]

    kotlin_version = "1.3.70"
    def version_okHttp = "3.12.6"
    def version_retrofit = "2.6.4"

    app_implementation = [
            "annotation"             : "androidx.annotation:annotation:1.0.2",
            "lifecycle-extensions"   : "androidx.lifecycle:lifecycle-extensions:2.0.0",
            "constraintlayout"       : "androidx.constraintlayout:constraintlayout:1.1.3",
            "appcompat"              : "androidx.appcompat:appcompat:1.1.0",
            "legacy-support"         : "androidx.legacy:legacy-support-v4:1.0.0",
            "recyclerview"           : "androidx.recyclerview:recyclerview:1.1.0",
            "cardview"               : "androidx.cardview:cardview:1.0.0",
            "gson"                   : "com.google.code.gson:gson:2.8.5",
            "eventbus"               : "org.greenrobot:eventbus:3.1.1",
            "viewpager2"             : "androidx.viewpager2:viewpager2:1.0.0",
            "kotlin"                 : "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version",
            "navigation-fragment"    : "androidx.navigation:navigation-fragment:2.2.2",
            "navigation-ui"          : "androidx.navigation:navigation-ui:2.2.2",
            "navigation-fragment-ktx": "androidx.navigation:navigation-fragment-ktx:2.2.2",
            "navigation-ui-ktx"      : "androidx.navigation:navigation-ui-ktx:2.2.2",
            "material"               : "com.google.android.material:material:1.1.0",
            "activity"               : "androidx.activity:activity:1.2.0-alpha05",
    ]

    supportDeps = [
            design           : 'com.google.android.material:material:1.1.0',
            multiDex         : 'androidx.multidex:multidex:2.0.1',
            appcompat        : 'androidx.appcompat:appcompat:1.1.0',
            annotation       : 'androidx.annotation:annotation:1.1.0',
            lifecycle        : 'androidx.lifecycle:lifecycle-extensions:2.2.0',
            cardView         : "androidx.cardview:cardview:1.0.0",
            recyclerView     : 'androidx.recyclerview:recyclerview:1.1.0',
            constraintLayout : 'androidx.constraintlayout:constraintlayout:1.1.3',
            coordinatorlayout: 'androidx.coordinatorlayout:coordinatorlayout:1.1.0',
            flexbox          : 'com.google.android:flexbox:2.0.1',
    ]

    okHttp = [
            okhttp: "com.squareup.okhttp3:okhttp:$version_okHttp",
            logger: "com.squareup.okhttp3:logging-interceptor:$version_okHttp"
    ]

    retrofit = [
            retrofit     : "com.squareup.retrofit2:retrofit:$version_retrofit",
            rxAdapter    : "com.squareup.retrofit2:adapter-rxjava2:$version_retrofit",
            gsonConverter: "com.squareup.retrofit2:converter-gson:$version_retrofit",
    ]

    networkLibs = okHttp.values() + retrofit.values()

    baseModulesLib = [networkLibs]

    android_library_biz = { projectLibrary, projectName, version_code = 1, version_name = "1.0" ->
        projectLibrary.apply {
            plugin 'com.android.library'
            plugin 'kotlin-android'
            plugin 'kotlin-android-extensions'
            plugin 'kotlin-kapt'
            plugin RouterPlugin
            plugin ModuleServicePlugin
        }
        projectLibrary.android {
            compileSdkVersion rootProject.ext.android.compileSdkVersion
            defaultConfig {
                minSdkVersion rootProject.ext.android.minSdkVersion
                targetSdkVersion rootProject.ext.android.targetSdkVersion
                versionCode version_code
                versionName version_name
                vectorDrawables.useSupportLibrary = true

                javaCompileOptions {
                    annotationProcessorOptions {
                        arguments = [project_name: projectName]
                    }
                }
            }
            compileOptions {
                sourceCompatibility 1.8
                targetCompatibility 1.8
            }
        }

        projectLibrary.dependencies {
            implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
            implementation baseModulesLib
            app_implementation.each {
                k, v -> implementation v
            }
            if (projectLibrary.name != 'lib_dirty') {
                implementation project(':lib_dirty')
            }
        }
    }

}