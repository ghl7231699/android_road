import com.ghl.plugin.ModuleServicePlugin
import com.ghl.plugin.RouterPlugin

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: RouterPlugin
apply plugin: ModuleServicePlugin

def app_android = rootProject.ext.android
def app_implementation = rootProject.ext.app_implementation

android {

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    compileSdkVersion app_android.compileSdkVersion

    defaultConfig {
        applicationId "com.mmc.sample"
        minSdkVersion app_android.minSdkVersion
        targetSdkVersion app_android.targetSdkVersion
        versionCode app_android.versionCode
        versionName app_android.versionName
        vectorDrawables.useSupportLibrary = true
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
            debuggable true
            testProguardFile 'proguard-test-rules.pro'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            java.srcDirs = ['src/main/java', 'src/main/aidl']
            resources.srcDirs = ['src/main/java', 'src/main/aidl']
            aidl.srcDirs = ['src/main/aidl']
        }
    }

    //flutter相关声明
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

def modules_biz = [
        ':biz_main',
        ':biz_login',
        ':biz_flutter',
        ':biz_user',
        ':androidservice',
        ':opt_track',
        ':hot_fix',
        ':flutter',
        ':lib_dirty',
        ':flutter_boost'
]

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    modules_biz.each { biz ->
        implementation project(biz)
    }
    // 更简洁的方式，由于我们config那边定义的是 map，遍历
    app_implementation.each {
        k, v -> implementation v
    }
}